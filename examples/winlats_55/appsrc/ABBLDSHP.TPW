#GROUP(%ConstructShipList)
#CREATE(%BuildFile)
#DECLARE(%ShipFile)
#SET(%ShipFile,%Application & '.shp')
#MESSAGE('Generating Module:    ' & %ShipFile,1) #! Post generation message
#MESSAGE('Generating Ship List',2) #! Post generation message
#MESSAGE('',3)
Clarion 5.5
-------------------
    Ship List

The following files must be shipped with your application.

NOTE:  This list only includes those files that are VISIBLE
       to the Clarion 5.5 templates.  Any DLLs loaded
       in EMBEDs or INCLUDE files may not be visible to the
       templates, and may not be in this list.

NOTE:  In the case of External Library modules, the .LIB file
       will be listed below.  Some of these .LIBs (WINDOWS.LIB
       for example) do not have DLLs associated with them.  In
       most cases you will need to ship an associated DLL.

NOTE:  In the case of an External Library module generated by
       AppGen, you must insure that all files on the shipping
       list for that LIB/DLL are also included.


___  Executable Program:  %Application.EXE
#SUSPEND
     #?DLLs associated with the following libraries.
#?
#FOR(%Module),WHERE(%ModuleExternal)
___    %Module

#ENDFOR
#RESUME

#IF (~%ApplicationLocalLibrary)
     The following Clarion 5.5 DLLs
--------------------------
  #IF(%Target32)
___    C55RUNX.DLL - Clarion 5.5 Library
  #ELSE
___    C55RUN.DLL - Clarion 5.5 Library
___    C55LFNX.DLL - Clarion 5.5 long file name support, in 16bit applications, for NT
  #ENDIF

  #FOR(%UsedDriverDLLs)
    #FIND(%DriverDLL,%UsedDriverDLLs)
    #SET (%ValueConstruct,INSTRING('.DLL',%DriverDLL,1))
    #IF(%Target32 AND %ValueConstruct)
___    %(SUB(%DriverDLL,1,%ValueConstruct-1))X%(SUB(%DriverDLL,%ValueConstruct)) - %DriverDescription
    #ELSE
___    %UsedDriverDLLs - %DriverDescription
    #ENDIF
  #ENDFOR
#ENDIF
#IF(ITEMS(%VBXList))
___    CWVBX.DLL - Clarion 5.5 VBX Support Library

     The following VBXs
-----------------------
  #FOR(%VBXList)
___       %VBXList
  #ENDFOR

#ENDIF
#IF(%OLENeeded)
  #IF (~%ApplicationLocalLibrary)
    #IF(%Target32)
___    C55OLEX.DLL - Clarion 5.5 OLE Support Library
    #ELSE
___    C55OLE.DLL - Clarion 5.5 OLE Support Library
    #ENDIF
  #ENDIF

  #IF(ITEMS(%OCXList))
     These files associated with OLE and OCX controls
-------------------------------------------------
    #FOR(%OCXList)
___       %OCXList
    #ENDFOR
  #ENDIF

     NOTE: It is possible that OLE and OCX files might not be in this list. Please
           take care to include all files used.

#ENDIF
  #EMBED(%ShipList,'Inside the Shipping List')
#CLOSE(%BuildFile)
#REPLACE(%ShipFile,%BuildFile)
